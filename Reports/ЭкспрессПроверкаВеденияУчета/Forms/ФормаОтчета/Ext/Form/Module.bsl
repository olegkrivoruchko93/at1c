
#Область ОбработчикиСобытийПоляТабличногоДокумента



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Метадод является переработанным вариантом одноименного метада из типовой БП 3.0
//
&НаКлиенте
Процедура ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка)
	
	// Получим параметры расшифровки
	НачалоПериодаРасшифровки 	= Отчет.НачалоПериода;
	Если Расшифровка.Свойство("НачалоПериода") Тогда
		НачалоПериодаРасшифровки 	= Расшифровка.НачалоПериода;
	КонецЕсли;
	
	КонецПериодаРасшифровки		= Отчет.КонецПериода;
	Если Расшифровка.Свойство("КонецПериода") Тогда
		КонецПериодаРасшифровки		= Расшифровка.КонецПериода;
	КонецЕсли;
	
	Счет	= Неопределено;
	Если Расшифровка.Свойство("Счет") Тогда
		Счет = Расшифровка.Счет;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказательНУ = Неопределено;
	ЗаполнятьПоказателиПоУмолчанию = Истина;
	Если Расшифровка.Свойство("ПоказательНУ") Тогда
		ПоказательНУ = Расшифровка.ПоказательНУ;
		ЗаполнятьПоказателиПоУмолчанию = Ложь;
	КонецЕсли;
	
	// Подготавливаем структуру для отбора необходимых параметров.
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("Организация", 		Отчет.Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода", 	НачалоПериодаРасшифровки);
	ДополнительныеСвойства.Вставить("КонецПериода", 	КонецПериодаРасшифровки);
	ДополнительныеСвойства.Вставить("Счет", 			Счет);
	//+kriv@, 24.102023 Изменение текста функции из типовой
	ДополнительныеСвойства.Вставить("ПоСубсчетам",      Истина);
	//+/kriv@, 24.102023
	Если ПоказательНУ <> Неопределено Тогда
		ДополнительныеСвойства.Вставить("ПоказательНУ", ПоказательНУ);
	КонецЕсли;
	
	Если Расшифровка.Свойство("Группировка") Тогда
		ДополнительныеСвойства.Вставить("Группировка", Расшифровка.Группировка);
	КонецЕсли;
	
	Если Расшифровка.Свойство("Отбор") Тогда
		
		ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
		
		Для Каждого ЭлементСтруктуры Из Расшифровка.Отбор Цикл
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЦикла;	
		
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимРасшифровки", 		Истина);
	ПараметрыФормы.Вставить("ВидРасшифровки", 			2); // Из пользовательских настроек
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ЗаполняемыеНастройки",		 Новый Структура("Показатели, Отбор, Группировка", ЗаполнятьПоказателиПоУмолчанию, Ложь, Ложь));
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",	 Истина);
	
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("РезультатОбработкаРасшифровки")
Процедура ДБ_РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;

		Идентификатор = "";
		Если Расшифровка.Свойство("Идентификатор") Тогда
			Идентификатор = Расшифровка.Идентификатор;
		КонецЕсли;

		// Раздел "Учетная политика"
		Если Идентификатор = "НеизменностьУчП_БУ" Тогда
			ОткрытьУчетнуюПолитикуОрганизации(Расшифровка);
		ИначеЕсли Идентификатор = "НеизменностьУчП_НУ" Тогда
			ОткрытьУчетнуюПолитикуОрганизацииНУ(Расшифровка);
		ИначеЕсли Идентификатор = "ОценкаРозницы" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "ДиректКостингИСчет26" Тогда
			ОткрытьОтчетПоПроводкам(Расшифровка);
		ИначеЕсли Идентификатор = "ПрименениеПБУ18Счета" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "НетНУприУСН" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "НетЕНВД" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "НаличиеСложногоНДС" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		КонецЕсли;

		// Раздел "Анализ состояния бухгалтерского учета"
		Если Идентификатор = "НетОстатковПоСчету000" Тогда
			ОткрытьАнализСчета(Расшифровка);
		ИначеЕсли Идентификатор = "НетОстатковПоСчетамПодлежащимЗакрытиюТипа1" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "НетОстатковПоСчетамПодлежащимЗакрытиюТипа2" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "СоответствиеОстатковИАктивностиСчетов" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "ПереоценкаВалютыНеУказанДокументРасчетовСКонтрагентом" Тогда
			ОткрытьВедомостьПоСчету(Расшифровка);
		ИначеЕсли Идентификатор = "ПереоценкаВалютыНеУказанаВалюта" Тогда
			ОткрытьВедомостьПоСчету(Расшифровка);
		ИначеЕсли Идентификатор = "ПереоценкаВалюты" Тогда
			ОткрытьВедомостьПоСчету(Расшифровка);
		ИначеЕсли Идентификатор = "ОкруглениеНачисленияНалогов" Тогда
			ОткрытьОтчетПоПроводкам(Расшифровка);
		КонецЕсли;

		// Раздел "Кассовые операции"
		Если Идентификатор = "МинусыПоКассе" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		ИначеЕсли Идентификатор = "ЛимитКассы" Тогда
			ОткрытьЛимитОстаткаКассыОрганизаций(Расшифровка);
		ИначеЕсли Идентификатор = "ЛимитРасчетовСКонтрагентами" Тогда
			ОткрытьОтчетПоПроводкам(Расшифровка);
		ИначеЕсли Идентификатор = "ВыдачаПодотчетПослеОтчета" Тогда
			ОткрытьКарточкуСчета(Расшифровка);
		КонецЕсли;   

		// Раздел "Уведомления ЕНС"
		Если Идентификатор = "СозданиеУведомленийЕНС" Тогда
			ОткрытьЗадачуБухгалтера(Расшифровка);
		ИначеЕсли Идентификатор = "СуммыУведомленийЕНС" Тогда
			ОткрытьЗадачуБухгалтера(Расшифровка);
		КонецЕсли;

#Удаление
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Строка") Тогда
#КонецУдаления
#Вставка
		// Раздел "Остатки на счетах бухгалтерского учета"
		Если Идентификатор = "ОтрицательныеОстаткиНаСчетахБУ" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		ИначеЕсли Идентификатор = "ОстаткиСчета10" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		ИначеЕсли Идентификатор = "ОстаткиСчета19" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		ИначеЕсли Идентификатор = "ОстаткиСчета41" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		ИначеЕсли Идентификатор = "ОстаткиСчета60" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		ИначеЕсли Идентификатор = "ОстаткиСчета62" Тогда
			ДБ_ОткрытьВедомостьПоСчетуССубсчетами(Расшифровка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Строка") Тогда
#КонецВставки
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти