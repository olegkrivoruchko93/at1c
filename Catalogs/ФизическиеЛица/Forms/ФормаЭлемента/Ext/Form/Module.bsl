#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ДБ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДБ_ИспользоватьПодсказкуDadata = ДБ_DaData.ИспользоватьПодсказкуDadata();
	
	ГруппаЗначенияКИГруппаКонтактнаяИнформация = "ГруппаЗначенияКИГруппаКонтактнаяИнформация";
	
	Если ДБ_ИспользоватьПодсказкуDadata И ЭтаФорма.Элементы[ГруппаЗначенияКИГруппаКонтактнаяИнформация] <> Неопределено Тогда
		
		Для Каждого Элемент_1 Из ЭтаФорма.Элементы[ГруппаЗначенияКИГруппаКонтактнаяИнформация].ПодчиненныеЭлементы Цикл
			
			Если Элемент_1.Заголовок = "Email" Тогда
				
				
				Для Каждого Элемент_2  Из Элемент_1.ПодчиненныеЭлементы Цикл
					
					Если СтрНачинаетсяС(Элемент_2.Имя, "ГруппаКомментарийКонтактнаяИнформацияПоле") Тогда
						
						Для Каждого Элемент_3  Из Элемент_2.ПодчиненныеЭлементы Цикл
							
							Если Элемент_3.Вид = ВидПоляФормы.ПолеВвода И СтрНачинаетсяС(Элемент_3.Имя, "КонтактнаяИнформацияПоле") Тогда
								Элемент_3.УстановитьДействие("АвтоПодбор", "ДБ_КонтактнаяИнформацияПолеEmailФизическиеЛицаАвтоПодборПосле");
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДБ_КонтактнаяИнформацияПолеEmailФизическиеЛицаАвтоПодборПосле(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Подсказки = ДБ_DaData.ПодсказкаПоEmail(Текст);
	
	Если Подсказки.suggestions.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Для Каждого email Из Подсказки.suggestions Цикл 
			ДанныеВыбора.Добавить(email.unrestricted_value, email.unrestricted_value);	
		КонецЦикла;
		
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ДБ_ФИОАвтоПодборПосле(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ДБ_ИспользоватьПодсказкуDadata Тогда
		
		Подсказки = ДБ_DaData.ПодсказкаПоФИО(Текст);
		
		Если Подсказки.suggestions.Количество() > 0 Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ДанныеВыбора = Новый СписокЗначений;
			
			Для Каждого ФИО Из Подсказки.suggestions Цикл 
				ДанныеВыбора.Добавить(ФИО.unrestricted_value, ФИО.unrestricted_value);	
			КонецЦикла;
			
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти