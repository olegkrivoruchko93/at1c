&НаСервере
Процедура ДБ_УстановитьПлоскийСтиль(Форма)
	
	ЦветРамки = Новый Цвет(230, 230, 230);
	Для Каждого Элемент Из Форма.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			Если Элемент.Вид = ВидГруппыФормы.Подменю Тогда
				Элемент.ОтображениеФигуры = ОтображениеФигурыКнопки.ПриАктивности;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Элемент) = Тип("КнопкаФормы") Тогда
			Если Не Элемент.КнопкаПоУмолчанию Тогда
				Элемент.ОтображениеФигуры = ОтображениеФигурыКнопки.ПриАктивности;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Элемент.ЦветРамки = ЦветРамки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&После("ПриСозданииНаСервере")
Процедура ДБ_ПриСозданииНаСервере(Форма, Знач ПараметрыРазмещения)
	
	Если Константы.ДБ_ИспользоватьНастройкуСтиля.Получить() Тогда
		Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиСтиля", "",, ИмяПользователя());
		Если ТипЗнч(Настройка) = Тип("Структура") Тогда
			ПлоскийСтиль = Неопределено;
			Если Настройка.Свойство("ПлоскийСтиль", ПлоскийСтиль) Тогда
				Если ПлоскийСтиль = Истина Тогда
					ДБ_УстановитьПлоскийСтиль(Форма);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры