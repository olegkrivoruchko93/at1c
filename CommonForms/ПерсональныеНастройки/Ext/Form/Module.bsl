

&НаСервере
Процедура ДБ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Если Константы.ДБ_ИспользоватьНастройкуСтиля.Получить() Тогда
		ДБ_ДобавитьИзменитьРеквизиты();
		ДБ_ВосстановитьНастройки();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДБ_ДобавитьИзменитьРеквизиты()
	
	ТипБулево = Новый ОписаниеТипов("Булево");

	Реквизит1 = Новый РеквизитФормы("ДБ_КомпактныйРежим", ТипБулево, "", "Компактный режим");
	Реквизит2 = Новый РеквизитФормы("ДБ_ПлоскийСтиль", ТипБулево, "", "Плоский стиль");
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Реквизит1);
	ДобавляемыеРеквизиты.Добавить(Реквизит2);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	ГруппаФормы = Элементы.Добавить("ПС_НастройкиСтиля", Тип("ГруппаФормы"));
	ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаФормы.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ГруппаФормы.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
	ГруппаФормы.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаФормы.Заголовок = "Настройки стиля";
	ГруппаФормы.Скрыть();
	
	ПолеФормы = Элементы.Добавить("ДБ_ПлоскийСтиль", Тип("ПолеФормы"), ГруппаФормы);
	ПолеФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ПолеФормы.ПутьКДанным = "ДБ_ПлоскийСтиль";
	ПолеФормы.УстановитьДействие("ПриИзменении", "ДБ_ПлоскийСтильПриИзменении");
	
	ПолеФормы = Элементы.Добавить("ДБ_КомпактныйРежим", Тип("ПолеФормы"), ГруппаФормы);
	ПолеФормы.Вид = ВидПоляФормы.ПолеФлажка;
	ПолеФормы.ПутьКДанным = "ДБ_КомпактныйРежим";
	ПолеФормы.УстановитьДействие("ПриИзменении", "ДБ_КомпактныйРежимПриИзменении");
	
	ПолеФормы = Элементы.Добавить("ДБ_НадписьВыборЦветаСтиля", Тип("ДекорацияФормы"), ГруппаФормы);
	ПолеФормы.Вид = ВидДекорацииФормы.Надпись;
	ПолеФормы.Заголовок = "Выбрать цвет стиля";
	ПолеФормы.Гиперссылка = Истина;
	ПолеФормы.УстановитьДействие("Нажатие", "ДБ_НадписьВыборЦветаСтиляНажатие");
			
КонецПроцедуры

&НаСервере
Процедура ДБ_ВосстановитьНастройки()
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, ИмяПользователя());
	Если ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда
		ЭтотОбъект["ДБ_КомпактныйРежим"] = Настройка.ВариантМасштабаФормКлиентскогоПриложения = ВариантМасштабаФормКлиентскогоПриложения.Компактный;
	КонецЕсли;
	
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиСтиля", "",, ИмяПользователя());
	Если ТипЗнч(Настройка) = Тип("Структура") Тогда
		ПлоскийСтиль = Неопределено;
		Если Не Настройка.Свойство("ПлоскийСтиль", ПлоскийСтиль) Тогда ПлоскийСтиль = Ложь; КонецЕсли;
		ЭтотОбъект["ДБ_ПлоскийСтиль"] = ПлоскийСтиль;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДБ_СохранитьНастройки()

	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, ИмяПользователя());
	Если Не ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда Настройка = Новый НастройкиКлиентскогоПриложения; КонецЕсли;
	Настройка.ВариантМасштабаФормКлиентскогоПриложения = ?(ЭтотОбъект["ДБ_КомпактныйРежим"], ВариантМасштабаФормКлиентскогоПриложения.Компактный, ВариантМасштабаФормКлиентскогоПриложения.Авто);
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "", Настройка,, ИмяПользователя());
	
	Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиСтиля", "",, ИмяПользователя());
	Настройка = Новый Структура("ПлоскийСтиль", ЭтотОбъект["ДБ_ПлоскийСтиль"]);
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиСтиля", "", Настройка,, ИмяПользователя());
	
КонецПроцедуры

&НаКлиенте
Процедура ДБ_ПлоскийСтильПриИзменении(Элемент)
	ДБ_СохранитьНастройки();
КонецПроцедуры 

&НаКлиенте
Процедура ДБ_КомпактныйРежимПриИзменении(Элемент)
	
	ДБ_СохранитьНастройки();
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ДБ_ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Для установки стиля потребуется перезагрузиться. Сделать это сейчас?'"), Режим,0);
	
КонецПроцедуры

&НаКлиенте
Процедура ДБ_ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
        ЗавершитьРаботуСистемы(Истина, Истина,);
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДБ_НадписьВыборЦветаСтиляНажатие(Элемент)
	ОткрытьФорму("ОбщаяФорма.ДБ_ФормаВыбораЦветаСтиля");
КонецПроцедуры